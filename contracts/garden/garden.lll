;;;; ==========================================================================
;;;; @title Garden
;;;; @notice Runs external contract code (sans first byte) through CREATE:
;;;;         plants the seed.
;;;;         A lot of the code was copied from `cloning-vat`, then tweaked for
;;;;         this particular use case.
;;;; @author Noel Maersk <veox>

(seq
  (include "../common.lll.inc")

  ;; ==========================================================================
  ;; MEMORY LAYOUT

  (def '*memloc-plant-addr* 0x00) ; store created contract's address here
  (def '*memloc-seed-addr*  0x20) ; will copy runtime bytecode from here
  (def '*memloc-seed-size*  0x40) ; size of runtime bytecode
  (def '*memloc-seed-code*  0x60) ; where the to-clone code will _start_

  ;; ==========================================================================
  ;; CONSTANTS

  ;; TODO: isn't it 0x7f?..
  (def '*max-precomp-addr* 0xff) ; addresses reserved for precompiles

  (def '*plant* 0x00000000) ; plant(address)

  (def '*event-planted* ; planted(address,address)
       0x4b9f42fe89b8e6ddd0f1686d85da84e8ef528d915998e18563d662f5eba78032)

  ;; ==========================================================================
  ;; INIT

  ; none!

  ;; ==========================================================================
  ;; CODE

  (returnlll
   (seq
     unpayable

     ;; call data can be either 4+32 bytes (web3-compliant)...
     (when (= 36 (calldatasize))
       (seq
         ;; check for matching web3 function selector
         (unless (= *plant* calldata-function-selector) (revert 0 0))
         ;; load address as-is (assume web3-compliant call data)
         (mstore *memloc-seed-addr* (calldataload 0x04))))
     ;; ...or 20 bytes (just the address)
     (when (= 20 (calldatasize))
       (seq
         ;; right-align 20 bytes in 32 bytes of memory (8*12==96)
         (mstore *memloc-seed-addr* (/ (calldataload 0x00) (exp 2 96)))))

     ;; must have determined source address by now; and won't clone precompiles
     (when (<= (mload *memloc-seed-addr*) *max-precomp-addr*) (revert 0 0))

     ;; get source's code size (remove first REVERT byte)
     (mstore *memloc-seed-size* (- (extcodesize (mload *memloc-seed-addr*)) 1))
     ;; it must be non-zero
     (when (= 0 (mload *memloc-seed-size*)) (revert 0 0))

     ;; copy source code to memory
     (extcodecopy (mload *memloc-seed-addr*)  ; source address
                  *memloc-seed-code*          ; where to copy the code to
                  1                           ; where the code starts in source
                  (mload *memloc-seed-size*)) ; how much to copy

          ;; create new contract from memory, saving the destination's address
     (mstore
      *memloc-plant-addr*
      (create 0                            ; wei to transfer
              *memloc-seed-code*           ; where to copy the code from
              (mload *memloc-seed-size*))) ; how much to copy

     ;; destination address must be non-zero (i.e. CREATE succeeded)
     (when (= 0 (mload *memloc-plant-addr*)) (revert 0 0))

     ;; TODO: more checks before declaring it a success?..

     ;; success!
     (emit2 *event-planted*                   ; event id, indexed
            (caller)                          ; who requested the cloning, indexed
            (mload *memloc-seed-addr*)        ; what was cloned, indexed
            (mload *memloc-plant-addr*) 0x20) ; where it was cloned to, unindexed
     (return *memloc-plant-addr* 0x20))))
